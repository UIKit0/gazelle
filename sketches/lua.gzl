
Name: /[:alpha:_][:alnum:_]* ~
       (and|break|do|else|elseif|end|false|for|function|if|in|
        local|nil|not|or|repeat|return|then|true|until|while)/

Number: /[0-9]* (\. [0-9]+)? ([Ee] [+-]? [0-9]+)? | 0x[0-9A-Fa-f]+/;

chunk ->  (stat ";"?) *  (laststat ";"?) ? ;
block -> chunk;

stat[assign]    -> varlist "=" explist;
stat[funccall]  -> functioncall;
stat[do]        -> "do" block "end";
stat[while]     -> "while" exp "do" block "end";
stat[repeat]    -> "repeat" block "until" exp;
stat[if]        -> "if" exp "then" block ("elseif" exp "then" block)* ("else" block)? "end";
stat[for]       -> "for" Name "=" exp "," exp ("," exp)? "do" block "end";
stat[funcdef]   -> "function" funcname funcbody;
stat[localfunc] -> "local" "function" Name funcbody;
stat[localvars] -> "local" namelist ("=" explist)?

laststat -> "return" explist? | "break"

funcname -> Name +(.) (":" Name)?

varlist  -> var +(,)
var      -> Name | prefixexp "[" exp "]" | prefixexp "." Name
namelist -> Name +(,)

exp -> "nil" | "false" | "true" | Number | string | "..." | function |
       prefixexp | tableconstructor | @expr(ops)

@oplist ops: 2< "or",
             2< "and",
             2< "<" | 2< ">" | 2< "<=" | 2< ">=" | 2< "~=" | 2< "==",
             2> "..",
             2< "+" | < "-",
             2< "*" | < "/" | < "%",
             1< "not" | 1< "#" | 1< "-",
             2> "^";

prefixexp    -> var | functioncall | "(" exp ")"
functioncall -> prefixexp args | prefixexp ":" Name args
args         -> "(" explist? ")" | tableconstructor | String

function     -> "function" funcbody
funcbody     -> "(" parlist? ")" block "end"
parlist      -> namelist "," "..." | "..."

tableconstructor -> "{" fieldlist? "}"
fieldlist -> field +(fieldsep) fieldsep?
field -> "[" exp "]" "=" exp | Name "=" exp | exp
fieldsep: /[,;]/

