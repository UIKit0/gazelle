
ident: /[b-zA-Z]+/;
whitespace -> .whitespace=/[\r\n\s\t]+/;

grammar     -> statement*;
statement   -> .nonterm=ident "->" derivations ";" | "@allow" ident "..." ident +(,) ";";
derivations -> derivation +("|");
derivation  -> term+;
single_term -> regex | string | .nonterm=ident;
term        -> ( "." .name=ident "=" )? (regex | string | .nonterm=ident ) modifier ? ;
modifier    -> "?" | "*" | "+" |
                ("*" | "+") "(" single_term ")";
string      -> '"' .dquote_str_body=/([^"\\]|\\.)*/ '"' |
               "'" .squote_str_body=/([^'\\]|\\.)*/ "'";
regex       -> "/" .regex_body=/([^\/\\]|\\.)*/ "/";

@allow whitespace grammar ... modifier, string, regex;
@start grammar;
